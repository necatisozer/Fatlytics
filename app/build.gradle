import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0
def versionStage = "alpha"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.fatlytics.app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!versionStage.isEmpty()) {
            versionName "${versionName}-${versionStage}"
        }
        archivesBaseName = "fatlytics-app-$versionName-$versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField 'String', "FATSECRET_API_BASE_URL", getLocalProperty("FATSECRET_API_BASE_URL")
        buildConfigField 'String', "FATSECRET_CONSUMER_KEY", getLocalProperty("FATSECRET_CONSUMER_KEY")
        buildConfigField 'String', "FATSECRET_CONSUMER_SECRET", getLocalProperty("FATSECRET_CONSUMER_SECRET")
        buildConfigField 'String', "FATSECRET_AUTH_TOKEN", getLocalProperty("FATSECRET_AUTH_TOKEN")
        buildConfigField 'String', "FATSECRET_AUTH_SECRET", getLocalProperty("FATSECRET_AUTH_SECRET")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    dataBinding { enabled = true }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.annotation:annotation:1.1.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha05'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation 'androidx.core:core-ktx:1.2.0-alpha01'
    implementation 'androidx.fragment:fragment-ktx:1.1.0-alpha08'
    implementation 'androidx.collection:collection-ktx:1.1.0-rc01'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha01'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01'

    // Navigation
    implementation "android.arch.navigation:navigation-fragment:1.0.0"
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0'
    implementation "android.arch.navigation:navigation-ui:1.0.0"
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0'

    // Extensions
    implementation "com.louiscad.splitties:splitties-activities:3.0.0-alpha04"
    //implementation "com.louiscad.splitties:splitties-alertdialog-appcompat:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-bundle:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-collections:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-fragments:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-fragmentargs:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-material-colors:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-snackbar:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-systemservices:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-toast:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-views:3.0.0-alpha04"
    implementation "com.louiscad.splitties:splitties-views-appcompat:3.0.0-alpha04"

    // Google Services
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-common-ktx:16.1.0'
    implementation 'com.firebaseui:firebase-ui-auth:4.3.1'
    implementation 'com.google.firebase:firebase-database:16.1.0'
    implementation 'com.github.rosariopfernandes.fireXtensions:database:0.3.4'
    implementation 'com.google.guava:guava:27.1-android'

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.5.0"
    implementation 'se.akerfeldt:okhttp-signpost:1.1.0'
    implementation 'oauth.signpost:signpost-core:1.2.1.2'

    // Moshi
    implementation "com.squareup.moshi:moshi:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    implementation 'com.serjltt.moshi:moshi-lazy-adapters:2.2'

    // Dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'
    implementation 'com.google.dagger:dagger-android:2.22.1'
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    kapt 'com.google.dagger:dagger-android-processor:2.22.1'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Storage
    implementation 'io.paperdb:paperdb:2.6'

    // Util
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.jakewharton.threetenabp:threetenabp:1.2.0"
    implementation "com.wajahatkarim3.easyvalidation:easyvalidation-core:1.0.1"

    // View
    implementation 'com.afollestad.material-dialogs:core:2.8.1'
    // implementation 'com.afollestad.material-dialogs:input:2.8.1'
    // implementation 'com.afollestad.material-dialogs:files:2.8.1'
    // implementation 'com.afollestad.material-dialogs:color:2.8.1'
    implementation 'com.afollestad.material-dialogs:datetime:2.8.1'
    implementation 'com.afollestad.material-dialogs:lifecycle:2.8.1'

    // Debug
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Test
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.2.0-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-beta01'
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    return df.format(new Date())
}

apply plugin: 'com.google.gms.google-services'