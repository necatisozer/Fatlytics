apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0
def versionStage = "alpha"

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'com.fatlytics.app'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        if (!versionStage.isEmpty()) {
            versionName "${versionName}-${versionStage}"
        }
        archivesBaseName = "$applicationId-v$versionName($versionCode)"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dataBinding { enabled true }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'
    implementation 'androidx.fragment:fragment-ktx:1.2.0-alpha01'
    implementation 'androidx.collection:collection-ktx:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'

    // AndroidX Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$android_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$android_lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$android_lifecycle_version"

    // AndroidX Navigation
    implementation "androidx.navigation:navigation-fragment:$android_navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$android_navigation_version"
    implementation "androidx.navigation:navigation-ui:$android_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$android_navigation_version"

    // Extensions
    implementation "com.louiscad.splitties:splitties-activities:$splitties_version"
    implementation "com.louiscad.splitties:splitties-arch-lifecycle:$splitties_version"
    implementation "com.louiscad.splitties:splitties-bundle:$splitties_version"
    implementation "com.louiscad.splitties:splitties-collections:$splitties_version"
    implementation "com.louiscad.splitties:splitties-fragments:$splitties_version"
    implementation "com.louiscad.splitties:splitties-fragmentargs:$splitties_version"
    implementation "com.louiscad.splitties:splitties-material-colors:$splitties_version"
    implementation "com.louiscad.splitties:splitties-snackbar:$splitties_version"
    implementation "com.louiscad.splitties:splitties-systemservices:$splitties_version"
    implementation "com.louiscad.splitties:splitties-toast:$splitties_version"
    implementation "com.louiscad.splitties:splitties-views:$splitties_version"
    implementation "com.louiscad.splitties:splitties-views-appcompat:$splitties_version"

    // View
    implementation "com.afollestad.material-dialogs:core:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:input:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:files:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:color:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:datetime:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:bottomsheets:$material_dialogs_version"
    implementation "com.afollestad.material-dialogs:lifecycle:$material_dialogs_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.0.1'

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.8.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'
    implementation 'com.serjltt.moshi:moshi-lazy-adapters:2.2'

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0-RC3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Util
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.pakoito:RxPaper2:1.4.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
    implementation 'com.wajahatkarim3.easyvalidation:easyvalidation-core:1.0.1'

    // Debug
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Test
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}